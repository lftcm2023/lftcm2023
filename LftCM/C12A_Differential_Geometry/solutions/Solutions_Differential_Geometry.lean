import LftCM.C12A_Differential_Geometry.Lib
import Mathlib.Analysis.NormedSpace.Connected
import Mathlib.LinearAlgebra.TensorProduct
import Mathlib.Analysis.NormedSpace.Dual

/-!
# Differential Geometry

Acknowledgements: Based on the tutorial by S√©bastien Gou√´zel at LFTCM 2020.

## An overview of manifolds in Lean, discussing design decisions

What is a manifold?

(1) allow field other than `‚Ñù` or `‚ÑÇ`?
(2) allow infinite dimension?
(3) allow boundary?
(4) allow model space depending on the point of the manifold?

Bourbaki: 2, 4
Lean: 1, 2, 3

Manifold in Lean:

* charted space structure, i.e., set of partial homeomorphisms to a model space.
  This is data, fixed once and for all (and a typeclass)
* compatibility condition, i.e., the change of coordinates should belong to some subgroup
  of the group of partial homeomorphisms of the model space. This is Prop (and a typeclass).
  The same manifold can be at the same time an analytic manifold,
  a smooth manifold and a topological manifold (with the same fixed atlas).
* A charted space is a smooth manifold (with corners) if it is compatible with the smooth
  groupoid on the model space. To cover uniformly both situations with and without boundary,
  the smooth groupoid is with respect to a map `I : H ‚Üí E` (think of `H` as the half-space and
  `E` the full space) in the half-space situation and `id : E ‚Üí E` in the boundaryless situation.
  This map `I` is called a _model with corners_. The most standard ones
  (identity in `‚Ñù^n` and inclusion of half-space in `‚Ñù^n`) have dedicated notations:
  `ùì° n` and `ùì°‚àÇ n`.
-/

open Set ENat Manifold Metric FiniteDimensional Bundle Function
attribute [local instance] Real.fact_zero_lt_one

noncomputable section
section examples

section unitInterval
open unitInterval

#check I -- I is notation for the interval [0, 1]

/- the interval [0, 1] is modelled by two charts with model space [0, ‚àû),
so it is a topological manifold -/
example : ChartedSpace (EuclideanHalfSpace 1) I := by
  infer_instance

/- To state that it is a smooth manifold, we have to say that all coordinate changes
live in the groupoid of smooth maps -/
example : HasGroupoid I (contDiffGroupoid ‚àû (ùì°‚àÇ 1)) := by
  infer_instance

-- This is the same as saying that `I` forms a smooth manifold.
example : SmoothManifoldWithCorners (ùì°‚àÇ 1) I := by
  infer_instance

-- atlases are not maximal in general
#check (contDiffGroupoid ‚àû (ùì°‚àÇ 1)).maximalAtlas I

end unitInterval

-- the sphere in a finite-dimensional inner product space is a smooth manifold

variable (n : ‚Ñï) (E : Type*) [NormedAddCommGroup E]
  [InnerProductSpace ‚Ñù E] [Fact (finrank ‚Ñù E = n + 1)]
#check SmoothManifoldWithCorners (ùì° n) (sphere (0 : E) 1)

-- the map ùïä‚Åø ‚Ü™ ‚Ñù‚Åø‚Å∫¬π is smooth
example : Smooth (ùì° n) ùìò(‚Ñù, E) ((¬∑) : sphere (0 : E) 1 ‚Üí E) := by
  exact?

-- the circle is a Lie group
example : LieGroup (ùì° 1) circle := by
  infer_instance

/- Dicussing three (controversial?) design decisions

#### Partial homeomorphisms

What is a partial homeomorphism `f` between an open subset of `E` and an open subset of `F`?
This is notion is called `LocalHomeomorph` in Mathlib.
(1) a map defined on a Subtype: `f x` only makes sense for `x : f.source`;
(2) a map defined on the whole space `E`, but taking values in `Option F = F ‚à™ {junk}`,
  with `f x = junk` when `x ‚àâ f.source`;
(3) a map defined on the whole space `E`, taking values in `F`,
  and we don't care about its values outside of `f.source`.

Just like division by zero! But worse:

* issue with (1): you keep intersecting chart domains.
  But the Subtype `u ‚à© v` is not the same as the Subtype `v ‚à© u`,
  so you keep adding casts everywhere.
* issue with (2): if you want to say that a chart is smooth,
  then you define to define smooth functions between `Option E` and `Option F`
  when `E` and `F` are vector spaces. All notions need to be redefined with `Option`.
* issue with (3): it works perfectly well, but it makes mathematicians unhappy/uneasy
  (and it is *not* equivalent to (1) or (2) when one of the spaces is empty).

We pick (3) in Mathlib.
-/

#check Equiv -- bijections with a chosen inverse
#check LocalEquiv -- An equiv between a subset of the domain and a subset of the codomain
#check LocalHomeomorph -- A homeomorphism between open subsets of the domain and codomain


/-
#### Tangent vectors

What is a tangent vector (for a manifold `M` modelled on a vector space `E`)?
(1) An equivalence class of germs of curves
(2) A derivation
(3) Physicist point of view: I don't know what a tangent vector is, but I know in charts.
  Mathematician's interpretation: equivalence class of `(e, v)`
  where `e` is a chart at `x`, `v` a vector in the vector space,
  and `(e, v) ‚àº (e', v')` if `D(e' ‚àò e ‚Åª¬π) v = v'`
(4) ...

Issues:
(1) Pictures are pretty, but this doesn't bring anything compared to (3)
  when you go down to details.
  And what about boundaries, where you can only have a half-curve
(2) Need partitions of unity to show that this is local and coincides with the usual point of view.
  Doesn't work well in finite smoothness, nor in complex manifolds
(3) Fine, works in all situations, but requires a lot of work to define the equivalence classes,
  the topology, check that the topology is compatible with the vector space structure, and so on.
  In a vector space, the tangent space is not defeq to the vector space itself
(4) Pick one favorite chart at `x`, say `e_x`, and *define* the tangent space at `x` to be `E`,
  but "seen" in the chart `e_x` (this will show up in the definition of the derivative: the
  derivative of `f : M ‚Üí M'` at `x` is defined to be the derivative of the map
  `e_{f x} ‚àò f ‚àò e_x‚Åª¬π`). Works perfectly fine, but makes mathematicians unhappy/uneasy.

We pick (4) in Mathlib. In fact, in the definition of a manifold,
every point has a preferred chart associated to it.
-/
#check TangentSpace
#check TangentBundle

/-
#### Smooth functions in manifolds with boundary

Usual definition of smooth functions in a half space: extend to a smooth function a little bit
beyond the boundary, so one only really needs to speak of smooth functions in open subsets of
vector spaces.

When you define the derivative, you will need to check that it does not depend on the choice
of the extension. Even worse when you want to define the tangent bundle: choose an open extension
of your manifold with boundary, and then check that the restriction of the tangent bundle does
not depend on the choice of the extension. Very easy when handwaving, nightmare to formalize.
(What is the extension of the manifold with boundary? Another type?)

Instead, if you define derivatives in (non-open) domains, you can talk of smooth functions in
domains, and do everything without extending. Need to know this early enough: when starting to
define derivatives, you should already think of manifolds with boundaries! That's what we did
in mathlib.

Difficulty: if a domain `s` is too small (think `s = ‚Ñù ‚äÜ ‚Ñù^2`), the values of `f` on `s` do not
prescribe uniquely a derivative, so `fderivWithinAt ‚Ñù f s x` may behave badly: the derivative of
a sum might be different from sum of derivatives, as there is an arbitrary choice to be made.
This does not happen with the half-space, as it is large enough: derivatives within domains only
work well if the tangent directions span the whole space. Predicate `UniqueDiffOn` for sets
in vector spaces. You won't find this in books!
-/
#check ContDiffWithinAt


-- declaring a smooth manifold is a little verbose:

variable {ùïú : Type*} [NontriviallyNormedField ùïú]

variable
  -- declare a smooth manifold `M` over the pair `(E, H)`.
  {E : Type*} [NormedAddCommGroup E] [NormedSpace ùïú E] {H : Type*} [TopologicalSpace H]
  (I : ModelWithCorners ùïú E H) {M : Type*} [TopologicalSpace M] [ChartedSpace H M]
  [SmoothManifoldWithCorners I M]
  -- declare a smooth manifold `N` over the pair `(F, G)`.
  {F : Type*} [NormedAddCommGroup F] [NormedSpace ùïú F] {G : Type*} [TopologicalSpace G]
  (J : ModelWithCorners ùïú F G) {N : Type*} [TopologicalSpace N] [ChartedSpace G N]
  [SmoothManifoldWithCorners J N]

example (f : M ‚Üí N) (x : M) : TangentSpace I x ‚ÜíL[ùïú] TangentSpace J (f x) :=
  mfderiv I J f x

example {f g : M ‚Üí M} (x : M)
    (hg : MDifferentiableAt I I g (f x)) (hf : MDifferentiableAt I I f x) :
    mfderiv I I (g ‚àò f) x = (mfderiv I I g (f x)).comp (mfderiv I I f x) :=
  mfderiv_comp x hg hf

example (f : M ‚Üí N) : TangentBundle I M ‚Üí TangentBundle J N :=
  tangentMap I J f

example [AddGroup N] [LieAddGroup J N] {f g : M ‚Üí N} {n : ‚Ñï‚àû}
    (hf : ContMDiff I J n f) (hg : ContMDiff I J n g) : ContMDiff I J n (f + g) :=
  hf.add hg

-- We also have smooth vector bundles

#check Trivialization
#check FiberBundle
#check VectorBundle
#check SmoothVectorBundle

variable
  {E : Type*} [NormedAddCommGroup E] [NormedSpace ùïú E] {H : Type*} [TopologicalSpace H]
  (IB : ModelWithCorners ùïú E H) {B : Type*} [TopologicalSpace B] [ChartedSpace H B]
  [SmoothManifoldWithCorners IB B]

-- Let `E‚ÇÅ` and `E‚ÇÇ` be smooth vector bundles over `B`

variable (F‚ÇÅ : Type*) [NormedAddCommGroup F‚ÇÅ] [NormedSpace ùïú F‚ÇÅ] (E‚ÇÅ : B ‚Üí Type*)
  [TopologicalSpace (TotalSpace F‚ÇÅ E‚ÇÅ)] [‚àÄ x, AddCommGroup (E‚ÇÅ x)] [‚àÄ x, Module ùïú (E‚ÇÅ x)]
  [‚àÄ x : B, TopologicalSpace (E‚ÇÅ x)] [FiberBundle F‚ÇÅ E‚ÇÅ] [VectorBundle ùïú F‚ÇÅ E‚ÇÅ]
  [SmoothVectorBundle F‚ÇÅ E‚ÇÅ IB]
variable (F‚ÇÇ : Type*) [NormedAddCommGroup F‚ÇÇ] [NormedSpace ùïú F‚ÇÇ] (E‚ÇÇ : B ‚Üí Type*)
  [TopologicalSpace (TotalSpace F‚ÇÇ E‚ÇÇ)] [‚àÄ x, AddCommGroup (E‚ÇÇ x)] [‚àÄ x, Module ùïú (E‚ÇÇ x)]
  [‚àÄ x : B, TopologicalSpace (E‚ÇÇ x)] [FiberBundle F‚ÇÇ E‚ÇÇ] [VectorBundle ùïú F‚ÇÇ E‚ÇÇ]
  [SmoothVectorBundle F‚ÇÇ E‚ÇÇ IB]


-- The product of two bundles is a smooth vector bundle.

example : SmoothVectorBundle (F‚ÇÅ √ó F‚ÇÇ) (E‚ÇÅ √ó·µá E‚ÇÇ) IB := by
  infer_instance

-- We can take construct the bundle of continuous linear maps between bundles.

variable [‚àÄ x, TopologicalAddGroup (E‚ÇÅ x)] [‚àÄ x, TopologicalAddGroup (E‚ÇÇ x)]
  [‚àÄ x, ContinuousSMul ùïú (E‚ÇÇ x)]


example : SmoothVectorBundle (F‚ÇÅ ‚ÜíL[ùïú] F‚ÇÇ) (Bundle.ContinuousLinearMap (.id ùïú) E‚ÇÅ E‚ÇÇ) IB := by
  infer_instance

-- We can pull back vector bundles.

variable (f : C^‚àû‚üÆI, M; IB, B‚üØ)

example : SmoothVectorBundle F‚ÇÅ ((f : M ‚Üí B) *·µñ E‚ÇÅ) I := by
  apply SmoothVectorBundle.pullback

/- Patrick Massot, Oliver Nash and I have proven sphere eversion from Gromov's h-principle -/

def Immersion (f : M ‚Üí N) : Prop := ‚àÄ m, Injective (mfderiv I J f m)

variable (E : Type*) [NormedAddCommGroup E] [InnerProductSpace ‚Ñù E]
  [Fact (finrank ‚Ñù E = 3)]

local notation "‚Ñù¬≥" => E
local notation "ùïä¬≤" => sphere (0 : ‚Ñù¬≥) 1

theorem sphere_eversion : ‚àÉ f : ‚Ñù ‚Üí ùïä¬≤ ‚Üí ‚Ñù¬≥,
    (ContMDiff (ùìò(‚Ñù, ‚Ñù).prod (ùì° 2)) ùìò(‚Ñù, ‚Ñù¬≥) ‚àû (uncurry f)) ‚àß
    (f 0 = Œª x : ùïä¬≤ ‚Ü¶ (x : ‚Ñù¬≥)) ‚àß
    (f 1 = Œª x : ùïä¬≤ ‚Ü¶ -(x : ‚Ñù¬≥)) ‚àß
    ‚àÄ t, Immersion (ùì° 2) ùìò(‚Ñù, ‚Ñù¬≥) (f t) :=
  sorry -- not yet in mathlib

end examples














/-! ## Exercises -/

/-! ### Local homeomorphisms

Local homeomorphisms are globally defined maps with a globally defined "inverse", but the only
relevant set is the *source*, which should be mapped homeomorphically to the *target*.
-/

-- set up a simple helper simp lemma to simplify our life later.
@[simp] lemma neg_mem_Ioo_minus_one_one (x : ‚Ñù) :
    -x ‚àà Ioo (-1 : ‚Ñù) 1 ‚Üî x ‚àà Ioo (-1 : ‚Ñù) 1 := by
  simp [neg_lt, and_comm]

/- Define a local homeomorphism from `‚Ñù` to `‚Ñù` which is just `x ‚Ü¶ -x`, but on `(-1, 1)`. In
Lean, the interval `(-1, 1)` is denoted by `Ioo (-1 : ‚Ñù) 1` (where `o` stands for _open_). -/

def myFirstLocalHomeo : LocalHomeomorph ‚Ñù ‚Ñù where
  toFun := fun x ‚Ü¶ -x
  invFun := fun x ‚Ü¶ -x
  source := Ioo (-1) 1
  target := Ioo (-1) 1
  map_source' := by
    intros x hx
    simp at hx
    simp [hx.1, hx.2, neg_lt (a := x)]
  map_target' := by
    intros x hx
    simp at hx
    simp [hx.1, hx.2, neg_lt (a := x)]
  left_inv' := by
    simp
  right_inv' := by
    simp
  open_source := isOpen_Ioo
  open_target := isOpen_Ioo
  continuous_toFun := continuous_neg.continuousOn
  continuous_invFun := continuous_neg.continuousOn
/- Two simple lemmas that will prove useful below. You can leave them sorried if you like. -/

lemma ne_3_of_mem_Ioo {x : ‚Ñù} (h : x ‚àà Ioo (-1 : ‚Ñù) 1) : x ‚â† 3 := by
  apply ne_of_lt
  trans (1 : ‚Ñù)
  ¬∑ exact h.2
  ¬∑ norm_num

lemma neg_ne_3_of_mem_Ioo {x : ‚Ñù} (h : x ‚àà Ioo (-1 : ‚Ñù) 1) : -x ‚â† 3 := by
  intro h'
  simp at h
  linarith

/- Now, define a second local homeomorphism which is almost like the previous one.  You may find the
following lemma useful for `continuous_toFun`: -/
#check ContinuousOn.congr


def mySecondLocalHomeo : LocalHomeomorph ‚Ñù ‚Ñù where
  toFun := fun x ‚Ü¶ if x = 3 then 0 else - x
  invFun := fun x ‚Ü¶ -x
  source := Ioo (-1) 1
  target := Ioo (-1) 1
  map_source' := fun x hx ‚Ü¶ by simp [hx.1, hx.2, neg_lt (a := x), ne_3_of_mem_Ioo hx]
  map_target' := fun x hx ‚Ü¶ by simp [hx.1, hx.2, neg_lt (a := x)]
  left_inv' := fun x hx ‚Ü¶ by simp [hx.1, hx.2, neg_lt (a := x), ne_3_of_mem_Ioo hx]
  right_inv' := fun x hx ‚Ü¶ by simp [hx.1, hx.2, neg_lt (a := x), neg_ne_3_of_mem_Ioo hx]
  open_source := isOpen_Ioo
  open_target := isOpen_Ioo
  continuous_toFun := by
    dsimp only
    refine continuous_neg.continuousOn.congr (fun x hx ‚Ü¶ ?_)
    simp [hx.1, hx.2, neg_lt (a := x), ne_3_of_mem_Ioo hx]
  continuous_invFun := continuous_neg.continuousOn

/- Although the two above local homeos are the same for all practical purposes as they coincide
where relevant, they are not *equal*: -/

lemma myFirstLocalHomeo_ne_mySecondLocalHomeo :
    myFirstLocalHomeo ‚â† mySecondLocalHomeo := by
  intro h
  have : myFirstLocalHomeo 3 = mySecondLocalHomeo 3 := by rw [h]
  simp [myFirstLocalHomeo, mySecondLocalHomeo] at this

/- The right equivalence relation for local homeos is not equality, but `EqOnSource`.
Indeed, the two local homeos we have defined above coincide from this point of view. -/

#check LocalHomeomorph.EqOnSource

lemma EqOnSource_myFirstLocalHomeo_mySecondLocalHomeo :
    myFirstLocalHomeo.EqOnSource mySecondLocalHomeo := by
  refine ‚ü®rfl, fun x hx ‚Ü¶ ?_‚ü©
  simp [myFirstLocalHomeo, mySecondLocalHomeo, ne_3_of_mem_Ioo hx]

/-! ### An example of a charted space structure on `‚Ñù`

A charted space is a topological space together with a set of local homeomorphisms to a model space,
whose sources cover the whole space. For instance, `‚Ñù` is already endowed with a charted space
structure with model space `‚Ñù`, where the unique chart is the identity:
-/

#check chartedSpaceSelf ‚Ñù

/- For educational purposes only, we will put another charted space structure on `‚Ñù` using the
local homeomorphisms we have constructed above. To avoid using too much structure of `‚Ñù` (and to
avoid confusing Lean), we will work with a copy of `‚Ñù`, on which we will only register the
topology. -/

def My‚Ñù : Type := ‚Ñù
deriving OrderedRing, TopologicalSpace

@[simps]
instance chartedSpaceMy‚Ñù : ChartedSpace ‚Ñù My‚Ñù where
  atlas := { LocalHomeomorph.refl ‚Ñù, myFirstLocalHomeo }
  chartAt := fun x ‚Ü¶ if x ‚àà Ioo (-1) 1 then myFirstLocalHomeo else LocalHomeomorph.refl ‚Ñù
  mem_chart_source := by
    intro x
    dsimp only
    split_ifs with h
    ¬∑ exact h
    ¬∑ exact mem_univ _
  chart_mem_atlas := by
    intro x
    dsimp only
    split_ifs
    ¬∑ simp
    ¬∑ simp


/- Now come more interesting bits. We have endowed `My‚Ñù` with a charted space structure, with charts
taking values in `‚Ñù`. We want to say that this is a smooth structure, i.e., the changes of
coordinates are smooth. In Lean, this is written with `has_groupoid`. A groupoid is a set
of local homeomorphisms of the model space (for example, local homeos that are smooth on their
domain). A charted space admits the groupoid as a structure groupoid if all the changes of
coordinates belong to the groupoid.

There is a difficulty that the definitions are set up to be able to also speak of smooth manifolds
with boundary or with corners, so the name of the smooth groupoid on `‚Ñù` has the slightly strange
name `contDiffGroupoid ‚àû (modelWithCornersSelf ‚Ñù ‚Ñù)`. To avoid typing again and again
`modelWithCornersSelf ‚Ñù ‚Ñù`, let us introduce a shortcut
-/

@[reducible]
def ùì°1 := modelWithCornersSelf ‚Ñù ‚Ñù

/- In the library, there are such shortcuts for manifolds modelled on `‚Ñù^n`, denoted with `ùì° n`,
but for `n = 1` this does not coincide with the above one, as `‚Ñù^1` (a.k.a. `Fin 1 ‚Üí ‚Ñù`) is not
the same as `‚Ñù`! Still, since they are of the same nature, the notation we have just introduced
is very close, compare `ùì°1` with `ùì° 1` (and try not to get confused): -/

instance smooth_My‚Ñù : HasGroupoid My‚Ñù (contDiffGroupoid ‚àû ùì°1) := by
  -- in theory, we should prove that all compositions of charts are diffeos, i.e., they are smooth
  -- and their inverse are smooth. For symmetry reasons, it suffices to check one direction
  apply hasGroupoid_of_pregroupoid
  dsimp only
  -- take two charts `e` and `e'`
  intro e e' he he'
  simp [atlas] at he he' ‚ä¢
  -- to continue, some hints:
  -- (1) don't hesitate to use the fact that the restriction of a smooth function to a
  -- subset is still smooth there (`ContDiff.contDiffOn`)
  -- (2) hopefully, there is a theorem saying that the negation function is smooth.
  -- you can either try to guess its name, or hope that `apply?` will help you there.
  rcases he with rfl|rfl <;> rcases he' with rfl|rfl
  ¬∑ exact contDiff_id.contDiffOn
  ¬∑ exact contDiff_id.neg.contDiffOn
  ¬∑ exact contDiff_id.neg.contDiffOn
  ¬∑ convert contDiff_id.contDiffOn
    ext x
    simp [myFirstLocalHomeo]


/- The statement of the previous instance is not very readable. There is a shortcut notation: -/

instance : SmoothManifoldWithCorners ùì°1 My‚Ñù := { smooth_My‚Ñù with }

/- We will now study a very simple map from `My‚Ñù` to `‚Ñù`, the identity. -/
def myMap : My‚Ñù ‚Üí ‚Ñù := fun x ‚Ü¶ x

/- The map `myMap` is a map going from the type `My‚Ñù` to the type `‚Ñù`. From the point of view of
the kernel of Lean, it is just the identity, but from the point of view of structures on `My‚Ñù`
and `‚Ñù` it might not be trivial, as we have registered different instances on these two types. -/

/- The continuity should be trivial, as the topologies on `My‚Ñù` and `‚Ñù` are definitionally the
same. So `continuous_id` might help. -/

lemma continuous_myMap : Continuous myMap := by
  exact continuous_id


/- Smoothness should not be obvious, though, as the manifold structures are not the same: the atlas
on `My‚Ñù` has two elements, while the atlas on `‚Ñù` has one single element.
Note that `My‚Ñù` is not a vector space, nor a normed space, so one can not ask whether `myMap`
is smooth in the usual sense (as a map between vector spaces): -/

-- lemma contDiff_myMap : ContDiff ‚Ñù ‚àû myMap := sorry

/- does not make sense (try uncommenting it!) However, we can ask whether `myMap` is a smooth
map between manifolds, i.e., whether it is smooth when read in the charts. When we mention the
smoothness of a map, we should always specify explicitly the model with corners we are using,
because there might be several around (think of a complex manifold that you may want to consider
as a real manifold, to talk about functions which are real-smooth but not holomorphic) -/

lemma contMDiff_myMap : ContMDiff ùì°1 ùì°1 ‚àû myMap := by
  -- put things in a nicer form. The simp-set `mfld_simps` registers many simplification rules for
  -- manifolds. `simp` is used heavily in manifold files to bring everything into manageable form.
  rw [contMDiff_iff]
  simp only [continuous_myMap, mfld_simps]
  -- simp has erased the chart in the target, as it knows that the only chart in the manifold `‚Ñù`
  -- is the identity.
  intro x
  simp [myMap, Function.comp]
  split_ifs
  ¬∑ exact contDiff_id.neg.contDiffOn
  ¬∑ exact contDiff_id.contDiffOn

/- Now, let's go to tangent bundles. We have a smooth manifold, so its tangent bundle should also
be a smooth manifold. -/

-- the type `TangentBundle ùì°1 My‚Ñù` makes sense
#check TangentBundle ùì°1 My‚Ñù

/- The tangent space above a point of `My‚Ñù` is just a one-dimensional vector space
(identified with `‚Ñù`).
So, one can prescribe an element of the tangent bundle as a pair (more on this below) -/
example : TangentBundle ùì°1 My‚Ñù := ‚ü®(4 : ‚Ñù), 0‚ü©

/- Type-class inference can construct the smooth manifold structure on the tangent bundle. -/
example : SmoothManifoldWithCorners (ùì°1.prod ùì°1) (TangentBundle ùì°1 My‚Ñù) := by
  infer_instance

/-
NB: the model space for the tangent bundle to a product manifold or a tangent space is not
`‚Ñù √ó ‚Ñù`, but a copy called `ModelProd ‚Ñù ‚Ñù`. Otherwise, `‚Ñù √ó ‚Ñù` would have two charted space
structures with model `‚Ñù √ó ‚Ñù`, the identity one and the product one, which are not definitionally
equal. And this would be bad.
-/
example : ChartedSpace (ModelProd ‚Ñù ‚Ñù) (TangentBundle ùì°1 My‚Ñù) := by
  infer_instance

/-
The charts of any smooth vector bundle are characterized by `FiberBundle.chartedSpace_chartAt`
-/
#check @FiberBundle.chartedSpace_chartAt

/- A smooth map between manifolds induces a map between their tangent bundles. In `mathlib` this is
called the `tangentMap` (you might instead know it as the "differential" or "pushforward" of the
map).  Let us check that the `tangentMap` of `myMap` is smooth. -/
lemma ContMDiff_tangentMap_myMap :
  ContMDiff (ùì°1.prod ùì°1) (ùì°1.prod ùì°1) ‚àû (tangentMap ùì°1 ùì°1 myMap) := by
  -- hopefully, there is a theorem providing the general result, i.e. the tangent map to a smooth
  -- map is smooth.
  -- you can either try to guess its name, or hope that `apply?` will help you there.
  exact contMDiff_myMap.contMDiff_tangentMap le_top

/- (Harder question) Can you show that this tangent bundle is homeomorphic to `‚Ñù √ó ‚Ñù`? You could
try to build the homeomorphism by hand, using `tangentMap ùì°1 ùì°1 myMap` in one direction and a
similar map in the other direction, but it is probably more efficient to use one of the charts of
the tangent bundle.

Remember, the model space for `TangentBundle ùì°1 My‚Ñù` is `ModelProd ‚Ñù ‚Ñù`, not `‚Ñù √ó ‚Ñù`. But the
topologies on `ModelProd ‚Ñù ‚Ñù` and `‚Ñù √ó ‚Ñù` are the same, so it is by definition good enough to
construct a homeomorphism with `ModelProd ‚Ñù ‚Ñù`.
 -/

def myHomeo : TangentBundle ùì°1 My‚Ñù ‚âÉ‚Çú (‚Ñù √ó ‚Ñù) := by
  let p : TangentBundle ùì°1 My‚Ñù := ‚ü®(4 : ‚Ñù), 0‚ü©
  let F := chartAt (ModelProd ‚Ñù ‚Ñù) p
  have A : ¬¨ (4 : My‚Ñù) < 1 := by norm_num
  have S : F.source = univ
  ¬∑ simp [FiberBundle.chartedSpace_chartAt, A]
  have T : F.target = univ
  ¬∑ simp [FiberBundle.chartedSpace_chartAt, LocalHomeomorph.refl_target ‚Ñù, A]
  exact F.toHomeomorphOfSourceEqUnivTargetEqUniv S T

/- Up to now, we have never used the definition of the tangent bundle, and this corresponds to
the usual mathematical practice: one doesn't care if the tangent space is defined using germs of
curves, or spaces of derivations, or whatever equivalent definition. Instead, one relies all the
time on functoriality (i.e., a smooth map has a well defined derivative, and they compose well,
together with the fact that the tangent bundle to a vector space is the product).

If you want to know more about the internals of the tangent bundle in mathlib, you can browse
through the next section, but it is maybe wiser to skip it on first reading, as it is not needed
to use the library
-/

section you_should_probably_skip_this

/- If `M` is a manifold modelled on a vector space `E`, then tangent bundle is defined as
  `TotalSpace E (TangentSpace I : M ‚Üí Type _)`. An element of this is a pair `‚ü®x, v‚ü©` with `x : M`
  and `v : TangentSpace I x`.
  Here `TangentSpace I x` is just (a copy of) `E` by definition. -/

lemma tangentBundle_my‚Ñù_eq_totalSpace :
    TangentBundle ùì°1 My‚Ñù = Bundle.TotalSpace ‚Ñù (fun _x : My‚Ñù ‚Ü¶ ‚Ñù) :=
  rfl

/- However, in general, a tangent bundle is not trivial:
the topology on `TangentBundle ùì°1 My‚Ñù` is *not* the product topology.
Instead, the tangent space at a point `x` is identified
with `‚Ñù` through some preferred chart at `x`, called `chartAt ‚Ñù x`,
but the way they are glued together depends on the manifold and the charts.

In vector spaces, the tangent space is canonically the product space, with the same topology, as
there is only one chart so there is no strange gluing at play. The fact that the canonical map
from the sigma type to the product type (called `equiv.sigma_equiv_prod`) is a homeomorphism is
given in the library by `tangentBundleModelSpaceHomeomorph` (note that this is a definition,
constructing the homeomorphism, instead of a proposition asserting that `equiv.sigma_equiv_prod`
is a homeomorphism, because we use bundled homeomorphisms in mathlib).

Let us register the identification explicitly, as a homeomorphism. You can use the relevant fields
of `tangentBundleModelSpaceHomeomorph` to fill the nontrivial fields here.
-/

def tangentBundleVectorSpaceTriv (E : Type _) [NormedAddCommGroup E] [NormedSpace ‚Ñù E] :
  TangentBundle (modelWithCornersSelf ‚Ñù E) E ‚âÉ‚Çú E √ó E where
  toFun := fun p ‚Ü¶ (p.1, p.2)
  invFun := fun p ‚Ü¶ ‚ü®p.1, p.2‚ü©
  left_inv := by
    intro ‚ü®x, v‚ü©
    rfl
  right_inv := by
    intro ‚ü®x, v‚ü©
    rfl
  continuous_toFun := by
    exact (tangentBundleModelSpaceHomeomorph E (modelWithCornersSelf ‚Ñù E)).continuous
  continuous_invFun := by
    exact (tangentBundleModelSpaceHomeomorph E (modelWithCornersSelf ‚Ñù E)).continuous_invFun

/- Even though the tangent bundle to `My‚Ñù` is trivial abstractly, with this construction the
tangent bundle is *not* the product space with the product topology, as we have used various charts
so the gluing is not trivial. The following exercise unfolds the definition to see what is going on.
It is not a reasonable exercise, in the sense that one should never ever do this when working
with a manifold! -/

lemma crazy_formula_after_identifications (x : ‚Ñù) (v : ‚Ñù) :
    let p : TangentBundle ùì°1 My‚Ñù := ‚ü®(3 : ‚Ñù), 0‚ü©
    chartAt (ModelProd ‚Ñù ‚Ñù) p ‚ü®x, v‚ü© = if x ‚àà Ioo (-1 : ‚Ñù) 1 then (x, -v) else (x, v) := by
  -- this exercise is not easy (and shouldn't be: you are not supposed to use the library like this!)
  -- if you really want to do this, you should unfold as much as you can using simp and dsimp, until you
  -- are left with a statement speaking of derivatives of real functions, without any manifold code left.
  have : ¬¨ (3 : ‚Ñù) < 1 := by norm_num
  intro p
  simp [FiberBundle.chartedSpace_chartAt, TangentBundle.trivializationAt_apply]
  simp [My‚Ñù, this]
  split_ifs
  ¬∑ simp [myFirstLocalHomeo, fderiv_neg (f := fun x ‚Ü¶ x)]
  ¬∑ simp

end you_should_probably_skip_this

/-!
### The language of manifolds

In this paragraph, we will try to write down interesting statements of theorems, without proving them. The
goal here is that Lean should not complain on the statement, but the proof should be sorried.
-/

/- Here is a first example, already filled up, to show you how diffeomorphisms are currently named
(we will probably introduce an abbreviation, but this hasn't been done yet).
Don't try to fill the sorried proof! -/

/-- Two zero-dimensional connected manifolds are diffeomorphic. -/
theorem diffeomorph_of_zero_dim_connected
  (M M' : Type*) [TopologicalSpace M] [TopologicalSpace M']
  [ChartedSpace (EuclideanSpace ‚Ñù (Fin 0)) M] [ChartedSpace (EuclideanSpace ‚Ñù (Fin 0)) M']
  [ConnectedSpace M] [ConnectedSpace M'] :
  Nonempty (Structomorph (contDiffGroupoid ‚àû (ùì° 0)) M M') :=
sorry

/- Do you think that this statement is correct? (note that we have not assumed that our manifolds
are smooth, nor that they are separated, but this is maybe automatic in zero dimension).

Now, write down a version of this theorem in dimension 1, replacing the first sorry with meaningful content
(and adding what is needed before the colon).
Don't try to fill the sorried proof! -/

/-- Two one-dimensional smooth compact connected manifolds are diffeomorphic. -/
theorem diffeomorph_of_one_dim_compact_connected
    (M M' : Type*) [TopologicalSpace M] [TopologicalSpace M']
    [ChartedSpace (EuclideanSpace ‚Ñù (Fin 1)) M] [ChartedSpace (EuclideanSpace ‚Ñù (Fin 1)) M']
    [ConnectedSpace M] [ConnectedSpace M'] [CompactSpace M] [CompactSpace M']
    [T2Space M] [T2Space M']
    [SmoothManifoldWithCorners (ùì° 1) M] [SmoothManifoldWithCorners (ùì° 1) M'] :
    Nonempty (Structomorph (contDiffGroupoid ‚àû (ùì° 1)) M M') :=
  sorry

/- You will definitely need to require smoothness and separation in this case, as it is wrong otherwise.
Note that Lean won't complain if you don't put these assumptions, as the theorem would still make
sense, but it would just turn out to be wrong.

The previous statement is not really satisfactory: we would instead like to express that any such
manifold is diffeomorphic to the circle. The trouble is that we don't have the circle as a smooth
manifold yet. Since we have cheated and introduced it (with sorries) at the beginning of the tutorial,
let's cheat again and use it to reformulate the previous statement.
-/

notation "sphere" n => Metric.sphere (0 : EuclideanSpace ‚Ñù (Fin (n+1))) 1

-- The sphere is connected, compact and Hausdorff:

instance (n : ‚Ñï) : ConnectedSpace (sphere (n+1)) := by
  apply Subtype.connectedSpace
  apply isConnected_sphere
  ¬∑ simp [‚Üê finrank_eq_rank]
    norm_cast
    simp
  ¬∑ exact zero_le_one

example (n : ‚Ñï) : T2Space (sphere n) := by
  infer_instance

example (n : ‚Ñï) : CompactSpace (sphere n) := by
  infer_instance

instance (n : ‚Ñï) : Fact (finrank ‚Ñù (EuclideanSpace ‚Ñù (Fin (n+1))) = n + 1) := by
  constructor
  simp_rw [finrank_euclideanSpace, Fintype.card_fin]

/- Now, you can prove that any one-dimensional compact connected manifold is diffeomorphic to
the circle. Here, you should fill the `sorry` (but luckily you may use
`diffeomorph_of_one_dim_compact_connected`). -/
theorem diffeomorph_circle_of_one_dim_compact_connected
    (M : Type*) [TopologicalSpace M] [ChartedSpace (EuclideanSpace ‚Ñù (Fin 1)) M]
    [ConnectedSpace M] [CompactSpace M] [T2Space M] [SmoothManifoldWithCorners (ùì° 1) M] :
    Nonempty (Structomorph (contDiffGroupoid ‚àû (ùì° 1)) M (sphere 1)) := by
  exact diffeomorph_of_one_dim_compact_connected M (sphere 1)


/- What about trying to say that there are uncountably many different smooth structures on `‚Ñù‚Å¥`?
(see https://en.wikipedia.org/wiki/Exotic_R4). The library is not really designed with this in mind,
as in general we only work with one differentiable structure on a space, but it is perfectly
capable of expressing this fact if one uses the `@` version of some definitions.

Don't try to fill the sorried proof!
-/
theorem exotic_‚Ñù4 :
    let E := (EuclideanSpace ‚Ñù (Fin 4))
    ‚àÉ f : ‚Ñù ‚Üí ChartedSpace E E,
    ‚àÄ i, @HasGroupoid E _ E _ (f i) (contDiffGroupoid ‚àû (ùì° 4))
    ‚àß ‚àÄ i j, Nonempty (@Structomorph _ _ (contDiffGroupoid ‚àû (ùì° 4)) E E _ _ (f i) (f j)) ‚Üí
      i = j :=
  sorry

/-!
### Smooth functions on `[0, 1]`

In the following exercises we will prove that the tangent bundle to `[0, 1]`
is homeomorphic to `[0, 1] √ó ‚Ñù`. This is math-trivial but Lean-nontrivial.
These facts should also be Lean-trivial, but they are not (yet) since there is essentially
nothing in this direction for now in the library.

The goal is as much to be able to write the statements as to prove them. Most of the necessary vocabulary
has been introduced above, so don't hesitate to browse the file if you are stuck. Additionally, you will
need the notion of a smooth function on a subset: it is `ContDiffOn` for functions between vector
spaces and `ContMDiffOn` for functions between manifolds.

A global advice: don't hesitate to use and abuse `simp`, it is the main workhorse in this
area of mathlib.
-/

open unitInterval


def g : I ‚Üí ‚Ñù := Subtype.val

-- smoothness results for `EuclideanSpace` are expressed for general `L^p` spaces
-- (as `EuclideanSpace` has the `L^2` norm), in:
#check PiLp.contDiff_coord
#check PiLp.contDiffOn_iff_coord

lemma contMDiffG : ContMDiff (ùì°‚àÇ 1) ùì°1 ‚àû g := by
  rw [contMDiff_iff]
  refine ‚ü®continuous_subtype_val, fun x y ‚Ü¶ ?_‚ü©
  by_cases h : (x : ‚Ñù) < 1
  ¬∑ simp [g, chartAt, modelWithCornersEuclideanHalfSpace, add_zero, if_true, h, IccLeftChart,
      tsub_zero, preimage_setOf_eq, Function.update_same, max_lt_iff, zero_lt_one]
    have : ContDiff ‚Ñù ‚ä§ (fun x : EuclideanSpace ‚Ñù (Fin 1) ‚Ü¶ x 0) := PiLp.contDiff_coord 0
    refine this.contDiffOn.congr (fun f hf ‚Ü¶ ?_)
    obtain ‚ü®hf‚ÇÄ, hf‚ÇÅ‚ü© : 0 ‚â§ f 0 ‚àß f 0 < 1 := by simpa using hf
    simp [min_eq_left hf‚ÇÅ.le, max_eq_left hf‚ÇÄ]
  ¬∑ simp [chartAt, h, IccRightChart, Function.comp, modelWithCornersEuclideanHalfSpace, dif_pos
      max_lt_iff, preimage_setOf_eq, sub_zero, Subtype.range_coe_subtype, if_false, Subtype.coe_mk
      g, mfld_simps]
    have : ContDiff ‚Ñù ‚ä§ (fun (x : EuclideanSpace ‚Ñù (Fin 1)) ‚Ü¶ 1 - x 0) :=
      contDiff_const.sub (PiLp.contDiff_coord 0)
    refine this.contDiffOn.congr (fun f hf ‚Ü¶ ?_)
    obtain ‚ü®hf‚ÇÄ, hf‚ÇÅ‚ü© : 0 ‚â§ f 0 ‚àß f 0 < 1 := by simpa using hf
    have : 0 ‚â§ 1 - f 0 := by linarith
    simpa [hf‚ÇÄ, g]

lemma msmooth_of_smooth {f : ‚Ñù ‚Üí I} {s : Set ‚Ñù} (h : ContDiffOn ‚Ñù ‚àû (fun x ‚Ü¶ (f x : ‚Ñù)) s) :
  ContMDiffOn ùì°1 (ùì°‚àÇ 1) ‚àû f s := by
  rw [contMDiffOn_iff]
  constructor
  ¬∑ have : Embedding (Subtype.val : I ‚Üí ‚Ñù) := embedding_subtype_val
    exact (Embedding.continuousOn_iff this).2 h.continuousOn
  simp only [mfld_simps]
  intro y
  by_cases hy : (y : ‚Ñù) < 1
  { simp [chartAt, modelWithCornersEuclideanHalfSpace, Function.comp, hy, IccLeftChart,
      PiLp.contDiffOn_iff_coord, Fin.forall_fin_one]
    apply h.mono (inter_subset_left _ _) }
  { simp [chartAt, modelWithCornersEuclideanHalfSpace, Function.comp, hy, IccRightChart,
      PiLp.contDiffOn_iff_coord, Fin.forall_fin_one]
    apply (contDiffOn_const.sub h).mono (inter_subset_left _ _) }

/- A function from `‚Ñù` to `[0,1]` which is the identity on `[0,1]`. -/
def f : ‚Ñù ‚Üí I :=
  fun x ‚Ü¶ ‚ü®max (min x 1) 0, by simp [le_refl, zero_le_one]‚ü©

lemma contMDiffOnF : ContMDiffOn ùì°1 (ùì°‚àÇ 1) ‚àû f I := by
  apply msmooth_of_smooth
  apply contDiff_id.contDiffOn.congr
  intro x hx
  simp at hx
  simp [f, hx]

lemma fog : f ‚àò g = id := by
  ext x
  rcases x with ‚ü®x', h'‚ü©
  simp at h'
  simp [f, g, h']

lemma gof : ‚àÄ x ‚àà I, g (f x) = x := by
  intro x hx
  simp at hx
  simp [g, f]
  simp [hx]

def G : TangentBundle (ùì°‚àÇ 1) I ‚Üí I √ó ‚Ñù :=
  fun p ‚Ü¶ (p.1, ((tangentBundleVectorSpaceTriv ‚Ñù) (tangentMap (ùì°‚àÇ 1) ùì°1 g p)).2)

lemma continuous_G : Continuous G := by
  refine (FiberBundle.continuous_proj (EuclideanSpace ‚Ñù (Fin 1)) (TangentSpace (ùì°‚àÇ 1))).prod_mk ?_
  refine continuous_snd.comp ?_
  apply (Homeomorph.continuous _).comp
  apply ContMDiff.continuous_tangentMap contMDiffG le_top

def F : I √ó ‚Ñù ‚Üí TangentBundle (ùì°‚àÇ 1) I :=
  fun p ‚Ü¶ tangentMapWithin ùì°1 (ùì°‚àÇ 1) f I ((tangentBundleVectorSpaceTriv ‚Ñù).symm (p.1, p.2))

lemma continuous_F : Continuous F := by
  rw [continuous_iff_continuousOn_univ]
  apply (contMDiffOnF.continuousOn_tangentMapWithin le_top _).comp
  ¬∑ apply ((tangentBundleVectorSpaceTriv ‚Ñù).symm.continuous.comp _).continuousOn
    apply (continuous_subtype_val.comp continuous_fst).prod_mk continuous_snd
  ¬∑ intro ‚ü®‚ü®x, hx‚ü©, v‚ü© _
    simp [tangentBundleVectorSpaceTriv]
    exact hx
  ¬∑ rw [uniqueMDiffOn_iff_uniqueDiffOn]
    exact uniqueDiffOn_Icc_zero_one

lemma FoG : F ‚àò G = id := by
  ext1 ‚ü®x, v‚ü©
  simp [F, G, tangentMapWithin, tangentBundleVectorSpaceTriv, f]
  constructor
  ¬∑ rcases x with ‚ü®x', h'‚ü©
    simp at h'
    simp [h']
  ¬∑ change (tangentMapWithin ùì°1 (ùì°‚àÇ 1) f I (tangentMap (ùì°‚àÇ 1) ùì°1 g ‚ü®x, v‚ü©)).snd = v
    rw [‚Üê tangentMapWithin_univ, ‚Üê tangentMapWithin_comp_at, fog, tangentMapWithin_univ,
      tangentMap_id]
    ¬∑ rfl
    ¬∑ apply contMDiffOnF.mdifferentiableOn le_top
      simpa [g] using x.2
    ¬∑ apply (contMDiffG.contMDiffAt.mdifferentiableAt le_top).mdifferentiableWithinAt
    ¬∑ intro z _
      simp [g]
    ¬∑ apply uniqueMDiffOn_univ _ (mem_univ _)

lemma GoF : G ‚àò F = id := by
  ext1 ‚ü®x, v‚ü©
  simp [F, G, tangentMapWithin, tangentBundleVectorSpaceTriv, f]
  dsimp
  constructor
  ¬∑ rcases x with ‚ü®x', h'‚ü©
    simp at h'
    simp [h']
  ¬∑ have A : UniqueMDiffWithinAt ùì°1 I (‚ü®(x : ‚Ñù), v‚ü© : TangentBundle ùì°1 ‚Ñù).proj
    ¬∑ rw [uniqueMDiffWithinAt_iff_uniqueDiffWithinAt]
      apply uniqueDiffOn_Icc_zero_one _ x.2
    change (tangentMap (ùì°‚àÇ 1) ùì°1 g (tangentMapWithin ùì°1 (ùì°‚àÇ 1) f I ‚ü®x, v‚ü©)).snd = v
    rw [‚Üê tangentMapWithin_univ, ‚Üê tangentMapWithin_comp_at _ _ _ subset_preimage_univ A]
    ¬∑ have : tangentMapWithin ùì°1 ùì°1 (g ‚àò f) I ‚ü®x, v‚ü©
             = tangentMapWithin ùì°1 ùì°1 (id : ‚Ñù ‚Üí ‚Ñù) I ‚ü®x, v‚ü© :=
        tangentMapWithin_congr gof _ x.2 A
      rw [this, tangentMapWithin_id _ A]
    ¬∑ apply contMDiffG.contMDiffOn.mdifferentiableOn le_top _ (mem_univ _)
    ¬∑ apply contMDiffOnF.mdifferentiableOn le_top _ x.2

def myTangentHomeo : TangentBundle (ùì°‚àÇ 1) I ‚âÉ‚Çú I √ó ‚Ñù := by
  exact
  { toFun := G
    invFun := F
    continuous_toFun := continuous_G
    continuous_invFun := continuous_F
    left_inv := fun p ‚Ü¶ show (F ‚àò G) p = id p by rw [FoG]
    right_inv := fun p ‚Ü¶ show (G ‚àò F) p = id p by rw [GoF] }


/-!
### Further things to do

(1) can you prove `diffeomorph_of_zero_dim_connected`?

(2) Try to express and then prove the local inverse theorem in real manifolds: if a map between
real manifolds (without boundary, modelled on a complete vector space) is smooth, then it is
a local homeomorphism around each point. We already have versions of this statement in mathlib
for functions between vector spaces, but this is very much a work in progress.

(3) What about trying to prove `diffeomorph_of_one_dim_compact_connected`? (I am not sure mathlib
is ready for this, as the proofs I am thinking of are currently a little bit too high-powered.
If you manage to do it, you should absolutely PR it!)

(4) Construct the tensor product of two vector bundles
  Remark: we have not endowed the tensor product of vector spaces with a topology yet,
  even if the vector spaces were finite-dimensional.
-/
