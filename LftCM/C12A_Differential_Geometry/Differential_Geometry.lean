import LftCM.C12A_Differential_Geometry.Lib
import Mathlib.Analysis.NormedSpace.Connected
import Mathlib.LinearAlgebra.TensorProduct
import Mathlib.Analysis.NormedSpace.Dual

/-!
# Differential Geometry

Acknowledgements: Based on the tutorial by SÃ©bastien GouÃ«zel at LFTCM 2020.

## An overview of manifolds in Lean, discussing design decisions

What is a manifold?

(1) allow field other than `â„` or `â„‚`?
(2) allow infinite dimension?
(3) allow boundary?
(4) allow model space depending on the point of the manifold?

Bourbaki: 2, 4
Lean: 1, 2, 3

Manifold in Lean:

* charted space structure, i.e., set of partial homeomorphisms to a model space.
  This is data, fixed once and for all (and a typeclass)
* compatibility condition, i.e., the change of coordinates should belong to some subgroup
  of the group of partial homeomorphisms of the model space. This is Prop (and a typeclass).
  The same manifold can be at the same time an analytic manifold,
  a smooth manifold and a topological manifold (with the same fixed atlas).
* A charted space is a smooth manifold (with corners) if it is compatible with the smooth
  groupoid on the model space. To cover uniformly both situations with and without boundary,
  the smooth groupoid is with respect to a map `I : H â†’ E` (think of `H` as the half-space and
  `E` the full space) in the half-space situation and `id : E â†’ E` in the boundaryless situation.
  This map `I` is called a _model with corners_. The most standard ones
  (identity in `â„^n` and inclusion of half-space in `â„^n`) have dedicated notations:
  `ğ“¡ n` and `ğ“¡âˆ‚ n`.
-/

open Set ENat Manifold Metric FiniteDimensional Bundle Function
attribute [local instance] Real.fact_zero_lt_one

noncomputable section
section examples

section unitInterval
open unitInterval

#check I -- I is notation for the interval [0, 1]

/- the interval [0, 1] is modelled by two charts with model space [0, âˆ),
so it is a topological manifold -/
example : ChartedSpace (EuclideanHalfSpace 1) I := by
  infer_instance

/- To state that it is a smooth manifold, we have to say that all coordinate changes
live in the groupoid of smooth maps -/
example : HasGroupoid I (contDiffGroupoid âˆ (ğ“¡âˆ‚ 1)) := by
  infer_instance

-- This is the same as saying that `I` forms a smooth manifold.
example : SmoothManifoldWithCorners (ğ“¡âˆ‚ 1) I := by
  infer_instance

-- atlases are not maximal in general
#check (contDiffGroupoid âˆ (ğ“¡âˆ‚ 1)).maximalAtlas I

end unitInterval

-- the sphere in a finite-dimensional inner product space is a smooth manifold

variable (n : â„•) (E : Type*) [NormedAddCommGroup E]
  [InnerProductSpace â„ E] [Fact (finrank â„ E = n + 1)]
#check SmoothManifoldWithCorners (ğ“¡ n) (sphere (0 : E) 1)

-- the map ğ•Šâ¿ â†ª â„â¿âºÂ¹ is smooth
example : Smooth (ğ“¡ n) ğ“˜(â„, E) ((Â·) : sphere (0 : E) 1 â†’ E) := by
  exact?

-- the circle is a Lie group
example : LieGroup (ğ“¡ 1) circle := by
  infer_instance

/- Dicussing three (controversial?) design decisions

#### Partial homeomorphisms

What is a partial homeomorphism `f` between an open subset of `E` and an open subset of `F`?
This is notion is called `LocalHomeomorph` in Mathlib.
(1) a map defined on a Subtype: `f x` only makes sense for `x : f.source`;
(2) a map defined on the whole space `E`, but taking values in `Option F = F âˆª {junk}`,
  with `f x = junk` when `x âˆ‰ f.source`;
(3) a map defined on the whole space `E`, taking values in `F`,
  and we don't care about its values outside of `f.source`.

Just like division by zero! But worse:

* issue with (1): you keep intersecting chart domains.
  But the Subtype `u âˆ© v` is not the same as the Subtype `v âˆ© u`,
  so you keep adding casts everywhere.
* issue with (2): if you want to say that a chart is smooth,
  then you define to define smooth functions between `Option E` and `Option F`
  when `E` and `F` are vector spaces. All notions need to be redefined with `Option`.
* issue with (3): it works perfectly well, but it makes mathematicians unhappy/uneasy
  (and it is *not* equivalent to (1) or (2) when one of the spaces is empty).

We pick (3) in Mathlib.
-/

#check Equiv -- bijections with a chosen inverse
#check LocalEquiv -- An equiv between a subset of the domain and a subset of the codomain
#check LocalHomeomorph -- A homeomorphism between open subsets of the domain and codomain


/-
#### Tangent vectors

What is a tangent vector (for a manifold `M` modelled on a vector space `E`)?
(1) An equivalence class of germs of curves
(2) A derivation
(3) Physicist point of view: I don't know what a tangent vector is, but I know in charts.
  Mathematician's interpretation: equivalence class of `(e, v)`
  where `e` is a chart at `x`, `v` a vector in the vector space,
  and `(e, v) âˆ¼ (e', v')` if `D(e' âˆ˜ e â»Â¹) v = v'`
(4) ...

Issues:
(1) Pictures are pretty, but this doesn't bring anything compared to (3)
  when you go down to details.
  And what about boundaries, where you can only have a half-curve
(2) Need partitions of unity to show that this is local and coincides with the usual point of view.
  Doesn't work well in finite smoothness, nor in complex manifolds
(3) Fine, works in all situations, but requires a lot of work to define the equivalence classes,
  the topology, check that the topology is compatible with the vector space structure, and so on.
  In a vector space, the tangent space is not defeq to the vector space itself
(4) Pick one favorite chart at `x`, say `e_x`, and *define* the tangent space at `x` to be `E`,
  but "seen" in the chart `e_x` (this will show up in the definition of the derivative: the
  derivative of `f : M â†’ M'` at `x` is defined to be the derivative of the map
  `e_{f x} âˆ˜ f âˆ˜ e_xâ»Â¹`). Works perfectly fine, but makes mathematicians unhappy/uneasy.

We pick (4) in Mathlib. In fact, in the definition of a manifold,
every point has a preferred chart associated to it.
-/
#check TangentSpace
#check TangentBundle

/-
#### Smooth functions in manifolds with boundary

Usual definition of smooth functions in a half space: extend to a smooth function a little bit
beyond the boundary, so one only really needs to speak of smooth functions in open subsets of
vector spaces.

When you define the derivative, you will need to check that it does not depend on the choice
of the extension. Even worse when you want to define the tangent bundle: choose an open extension
of your manifold with boundary, and then check that the restriction of the tangent bundle does
not depend on the choice of the extension. Very easy when handwaving, nightmare to formalize.
(What is the extension of the manifold with boundary? Another type?)

Instead, if you define derivatives in (non-open) domains, you can talk of smooth functions in
domains, and do everything without extending. Need to know this early enough: when starting to
define derivatives, you should already think of manifolds with boundaries! That's what we did
in mathlib.

Difficulty: if a domain `s` is too small (think `s = â„ âŠ† â„^2`), the values of `f` on `s` do not
prescribe uniquely a derivative, so `fderivWithinAt â„ f s x` may behave badly: the derivative of
a sum might be different from sum of derivatives, as there is an arbitrary choice to be made.
This does not happen with the half-space, as it is large enough: derivatives within domains only
work well if the tangent directions span the whole space. Predicate `UniqueDiffOn` for sets
in vector spaces. You won't find this in books!
-/
#check ContDiffWithinAt


-- declaring a smooth manifold is a little verbose:

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]

variable
  -- declare a smooth manifold `M` over the pair `(E, H)`.
  {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E] {H : Type*} [TopologicalSpace H]
  (I : ModelWithCorners ğ•œ E H) {M : Type*} [TopologicalSpace M] [ChartedSpace H M]
  [SmoothManifoldWithCorners I M]
  -- declare a smooth manifold `N` over the pair `(F, G)`.
  {F : Type*} [NormedAddCommGroup F] [NormedSpace ğ•œ F] {G : Type*} [TopologicalSpace G]
  (J : ModelWithCorners ğ•œ F G) {N : Type*} [TopologicalSpace N] [ChartedSpace G N]
  [SmoothManifoldWithCorners J N]

example (f : M â†’ N) (x : M) : TangentSpace I x â†’L[ğ•œ] TangentSpace J (f x) :=
  mfderiv I J f x

example {f g : M â†’ M} (x : M)
    (hg : MDifferentiableAt I I g (f x)) (hf : MDifferentiableAt I I f x) :
    mfderiv I I (g âˆ˜ f) x = (mfderiv I I g (f x)).comp (mfderiv I I f x) :=
  mfderiv_comp x hg hf

example (f : M â†’ N) : TangentBundle I M â†’ TangentBundle J N :=
  tangentMap I J f

example [AddGroup N] [LieAddGroup J N] {f g : M â†’ N} {n : â„•âˆ}
    (hf : ContMDiff I J n f) (hg : ContMDiff I J n g) : ContMDiff I J n (f + g) :=
  hf.add hg

-- We also have smooth vector bundles

#check Trivialization
#check FiberBundle
#check VectorBundle
#check SmoothVectorBundle

variable
  {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E] {H : Type*} [TopologicalSpace H]
  (IB : ModelWithCorners ğ•œ E H) {B : Type*} [TopologicalSpace B] [ChartedSpace H B]
  [SmoothManifoldWithCorners IB B]

-- Let `Eâ‚` and `Eâ‚‚` be smooth vector bundles over `B`

variable (Fâ‚ : Type*) [NormedAddCommGroup Fâ‚] [NormedSpace ğ•œ Fâ‚] (Eâ‚ : B â†’ Type*)
  [TopologicalSpace (TotalSpace Fâ‚ Eâ‚)] [âˆ€ x, AddCommGroup (Eâ‚ x)] [âˆ€ x, Module ğ•œ (Eâ‚ x)]
  [âˆ€ x : B, TopologicalSpace (Eâ‚ x)] [FiberBundle Fâ‚ Eâ‚] [VectorBundle ğ•œ Fâ‚ Eâ‚]
  [SmoothVectorBundle Fâ‚ Eâ‚ IB]
variable (Fâ‚‚ : Type*) [NormedAddCommGroup Fâ‚‚] [NormedSpace ğ•œ Fâ‚‚] (Eâ‚‚ : B â†’ Type*)
  [TopologicalSpace (TotalSpace Fâ‚‚ Eâ‚‚)] [âˆ€ x, AddCommGroup (Eâ‚‚ x)] [âˆ€ x, Module ğ•œ (Eâ‚‚ x)]
  [âˆ€ x : B, TopologicalSpace (Eâ‚‚ x)] [FiberBundle Fâ‚‚ Eâ‚‚] [VectorBundle ğ•œ Fâ‚‚ Eâ‚‚]
  [SmoothVectorBundle Fâ‚‚ Eâ‚‚ IB]


-- The product of two bundles is a smooth vector bundle.

example : SmoothVectorBundle (Fâ‚ Ã— Fâ‚‚) (Eâ‚ Ã—áµ‡ Eâ‚‚) IB := by
  infer_instance

-- We can take construct the bundle of continuous linear maps between bundles.

variable [âˆ€ x, TopologicalAddGroup (Eâ‚ x)] [âˆ€ x, TopologicalAddGroup (Eâ‚‚ x)]
  [âˆ€ x, ContinuousSMul ğ•œ (Eâ‚‚ x)]


example : SmoothVectorBundle (Fâ‚ â†’L[ğ•œ] Fâ‚‚) (Bundle.ContinuousLinearMap (.id ğ•œ) Eâ‚ Eâ‚‚) IB := by
  infer_instance

-- We can pull back vector bundles.

variable (f : C^âˆâŸ®I, M; IB, BâŸ¯)

example : SmoothVectorBundle Fâ‚ ((f : M â†’ B) *áµ– Eâ‚) I := by
  apply SmoothVectorBundle.pullback

/- Patrick Massot, Oliver Nash and I have proven sphere eversion from Gromov's h-principle -/

def Immersion (f : M â†’ N) : Prop := âˆ€ m, Injective (mfderiv I J f m)

variable (E : Type*) [NormedAddCommGroup E] [InnerProductSpace â„ E]
  [Fact (finrank â„ E = 3)]

local notation "â„Â³" => E
local notation "ğ•ŠÂ²" => sphere (0 : â„Â³) 1

theorem sphere_eversion : âˆƒ f : â„ â†’ ğ•ŠÂ² â†’ â„Â³,
    (ContMDiff (ğ“˜(â„, â„).prod (ğ“¡ 2)) ğ“˜(â„, â„Â³) âˆ (uncurry f)) âˆ§
    (f 0 = Î» x : ğ•ŠÂ² â†¦ (x : â„Â³)) âˆ§
    (f 1 = Î» x : ğ•ŠÂ² â†¦ -(x : â„Â³)) âˆ§
    âˆ€ t, Immersion (ğ“¡ 2) ğ“˜(â„, â„Â³) (f t) :=
  sorry -- not yet in mathlib

end examples














/-! ## Exercises -/

/-! ### Local homeomorphisms

Local homeomorphisms are globally defined maps with a globally defined "inverse", but the only
relevant set is the *source*, which should be mapped homeomorphically to the *target*.
-/

-- set up a simple helper simp lemma to simplify our life later.
@[simp] lemma neg_mem_Ioo_minus_one_one (x : â„) :
    -x âˆˆ Ioo (-1 : â„) 1 â†” x âˆˆ Ioo (-1 : â„) 1 := by
  sorry

/- Define a local homeomorphism from `â„` to `â„` which is just `x â†¦ -x`, but on `(-1, 1)`. In
Lean, the interval `(-1, 1)` is denoted by `Ioo (-1 : â„) 1` (where `o` stands for _open_). -/

def myFirstLocalHomeo : LocalHomeomorph â„ â„ where
  toFun := fun x â†¦ -x
  invFun := fun x â†¦ -x
  source := Ioo (-1) 1
  target := sorry
  map_source' := by
    sorry
  map_target' := by
    sorry
  left_inv' := by
    sorry
  right_inv' := by
    sorry
  open_source := sorry
  open_target := sorry
  continuous_toFun := sorry
  continuous_invFun := sorry
/- Two simple lemmas that will prove useful below. You can leave them sorried if you like. -/

lemma ne_3_of_mem_Ioo {x : â„} (h : x âˆˆ Ioo (-1 : â„) 1) : x â‰  3 := by

lemma neg_ne_3_of_mem_Ioo {x : â„} (h : x âˆˆ Ioo (-1 : â„) 1) : -x â‰  3 := by

/- Now, define a second local homeomorphism which is almost like the previous one.  You may find the
following lemma useful for `continuous_toFun`: -/
#check ContinuousOn.congr


def mySecondLocalHomeo : LocalHomeomorph â„ â„ where
  toFun := fun x â†¦ if x = 3 then 0 else - x
  invFun := fun x â†¦ -x
  source := Ioo (-1) 1
  target := sorry
  map_source' := sorry
  map_target' := sorry
  left_inv' := sorry
  right_inv' := sorry
  open_source := sorry
  open_target := sorry
  continuous_toFun := by
    sorry
  continuous_invFun := sorry

/- Although the two above local homeos are the same for all practical purposes as they coincide
where relevant, they are not *equal*: -/

lemma myFirstLocalHomeo_ne_mySecondLocalHomeo :
    myFirstLocalHomeo â‰  mySecondLocalHomeo := by

/- The right equivalence relation for local homeos is not equality, but `EqOnSource`.
Indeed, the two local homeos we have defined above coincide from this point of view. -/

#check LocalHomeomorph.EqOnSource

lemma EqOnSource_myFirstLocalHomeo_mySecondLocalHomeo :
    myFirstLocalHomeo.EqOnSource mySecondLocalHomeo := by

/-! ### An example of a charted space structure on `â„`

A charted space is a topological space together with a set of local homeomorphisms to a model space,
whose sources cover the whole space. For instance, `â„` is already endowed with a charted space
structure with model space `â„`, where the unique chart is the identity:
-/

#check chartedSpaceSelf â„

/- For educational purposes only, we will put another charted space structure on `â„` using the
local homeomorphisms we have constructed above. To avoid using too much structure of `â„` (and to
avoid confusing Lean), we will work with a copy of `â„`, on which we will only register the
topology. -/

def Myâ„ : Type := â„
deriving OrderedRing, TopologicalSpace

@[simps]
instance chartedSpaceMyâ„ : ChartedSpace â„ Myâ„ where
  atlas := { LocalHomeomorph.refl â„, myFirstLocalHomeo }
  chartAt := fun x â†¦ if x âˆˆ Ioo (-1) 1 then myFirstLocalHomeo else LocalHomeomorph.refl â„
  mem_chart_source := by
  chart_mem_atlas := by


/- Now come more interesting bits. We have endowed `Myâ„` with a charted space structure, with charts
taking values in `â„`. We want to say that this is a smooth structure, i.e., the changes of
coordinates are smooth. In Lean, this is written with `has_groupoid`. A groupoid is a set
of local homeomorphisms of the model space (for example, local homeos that are smooth on their
domain). A charted space admits the groupoid as a structure groupoid if all the changes of
coordinates belong to the groupoid.

There is a difficulty that the definitions are set up to be able to also speak of smooth manifolds
with boundary or with corners, so the name of the smooth groupoid on `â„` has the slightly strange
name `contDiffGroupoid âˆ (modelWithCornersSelf â„ â„)`. To avoid typing again and again
`modelWithCornersSelf â„ â„`, let us introduce a shortcut
-/

@[reducible]
def ğ“¡1 := modelWithCornersSelf â„ â„

/- In the library, there are such shortcuts for manifolds modelled on `â„^n`, denoted with `ğ“¡ n`,
but for `n = 1` this does not coincide with the above one, as `â„^1` (a.k.a. `Fin 1 â†’ â„`) is not
the same as `â„`! Still, since they are of the same nature, the notation we have just introduced
is very close, compare `ğ“¡1` with `ğ“¡ 1` (and try not to get confused): -/

instance smooth_Myâ„ : HasGroupoid Myâ„ (contDiffGroupoid âˆ ğ“¡1) := by
  -- in theory, we should prove that all compositions of charts are diffeos, i.e., they are smooth
  -- and their inverse are smooth. For symmetry reasons, it suffices to check one direction
  apply hasGroupoid_of_pregroupoid
  dsimp only
  -- take two charts `e` and `e'`
  intro e e' he he'
  simp [atlas] at he he' âŠ¢
  -- to continue, some hints:
  -- (1) don't hesitate to use the fact that the restriction of a smooth function to a
  -- subset is still smooth there (`ContDiff.contDiffOn`)
  -- (2) hopefully, there is a theorem saying that the negation function is smooth.
  -- you can either try to guess its name, or hope that `apply?` will help you there.


/- The statement of the previous instance is not very readable. There is a shortcut notation: -/

instance : SmoothManifoldWithCorners ğ“¡1 Myâ„ := { smooth_Myâ„ with }

/- We will now study a very simple map from `Myâ„` to `â„`, the identity. -/
def myMap : Myâ„ â†’ â„ := fun x â†¦ x

/- The map `myMap` is a map going from the type `Myâ„` to the type `â„`. From the point of view of
the kernel of Lean, it is just the identity, but from the point of view of structures on `Myâ„`
and `â„` it might not be trivial, as we have registered different instances on these two types. -/

/- The continuity should be trivial, as the topologies on `Myâ„` and `â„` are definitionally the
same. So `continuous_id` might help. -/

lemma continuous_myMap : Continuous myMap := by


/- Smoothness should not be obvious, though, as the manifold structures are not the same: the atlas
on `Myâ„` has two elements, while the atlas on `â„` has one single element.
Note that `Myâ„` is not a vector space, nor a normed space, so one can not ask whether `myMap`
is smooth in the usual sense (as a map between vector spaces): -/

-- lemma contDiff_myMap : ContDiff â„ âˆ myMap := sorry

/- does not make sense (try uncommenting it!) However, we can ask whether `myMap` is a smooth
map between manifolds, i.e., whether it is smooth when read in the charts. When we mention the
smoothness of a map, we should always specify explicitly the model with corners we are using,
because there might be several around (think of a complex manifold that you may want to consider
as a real manifold, to talk about functions which are real-smooth but not holomorphic) -/

lemma contMDiff_myMap : ContMDiff ğ“¡1 ğ“¡1 âˆ myMap := by
  -- put things in a nicer form. The simp-set `mfld_simps` registers many simplification rules for
  -- manifolds. `simp` is used heavily in manifold files to bring everything into manageable form.
  rw [contMDiff_iff]
  simp only [continuous_myMap, mfld_simps]
  -- simp has erased the chart in the target, as it knows that the only chart in the manifold `â„`
  -- is the identity.
  intro x

/- Now, let's go to tangent bundles. We have a smooth manifold, so its tangent bundle should also
be a smooth manifold. -/

-- the type `TangentBundle ğ“¡1 Myâ„` makes sense
#check TangentBundle ğ“¡1 Myâ„

/- The tangent space above a point of `Myâ„` is just a one-dimensional vector space
(identified with `â„`).
So, one can prescribe an element of the tangent bundle as a pair (more on this below) -/
example : TangentBundle ğ“¡1 Myâ„ := âŸ¨(4 : â„), 0âŸ©

/- Type-class inference can construct the smooth manifold structure on the tangent bundle. -/
example : SmoothManifoldWithCorners (ğ“¡1.prod ğ“¡1) (TangentBundle ğ“¡1 Myâ„) := by

/-
NB: the model space for the tangent bundle to a product manifold or a tangent space is not
`â„ Ã— â„`, but a copy called `ModelProd â„ â„`. Otherwise, `â„ Ã— â„` would have two charted space
structures with model `â„ Ã— â„`, the identity one and the product one, which are not definitionally
equal. And this would be bad.
-/
example : ChartedSpace (ModelProd â„ â„) (TangentBundle ğ“¡1 Myâ„) := by

/-
The charts of any smooth vector bundle are characterized by `FiberBundle.chartedSpace_chartAt`
-/
#check @FiberBundle.chartedSpace_chartAt

/- A smooth map between manifolds induces a map between their tangent bundles. In `mathlib` this is
called the `tangentMap` (you might instead know it as the "differential" or "pushforward" of the
map).  Let us check that the `tangentMap` of `myMap` is smooth. -/
lemma ContMDiff_tangentMap_myMap :
  ContMDiff (ğ“¡1.prod ğ“¡1) (ğ“¡1.prod ğ“¡1) âˆ (tangentMap ğ“¡1 ğ“¡1 myMap) := by
  -- hopefully, there is a theorem providing the general result, i.e. the tangent map to a smooth
  -- map is smooth.
  -- you can either try to guess its name, or hope that `apply?` will help you there.

/- (Harder question) Can you show that this tangent bundle is homeomorphic to `â„ Ã— â„`? You could
try to build the homeomorphism by hand, using `tangentMap ğ“¡1 ğ“¡1 myMap` in one direction and a
similar map in the other direction, but it is probably more efficient to use one of the charts of
the tangent bundle.

Remember, the model space for `TangentBundle ğ“¡1 Myâ„` is `ModelProd â„ â„`, not `â„ Ã— â„`. But the
topologies on `ModelProd â„ â„` and `â„ Ã— â„` are the same, so it is by definition good enough to
construct a homeomorphism with `ModelProd â„ â„`.
 -/

def myHomeo : TangentBundle ğ“¡1 Myâ„ â‰ƒâ‚œ (â„ Ã— â„) := by

/- Up to now, we have never used the definition of the tangent bundle, and this corresponds to
the usual mathematical practice: one doesn't care if the tangent space is defined using germs of
curves, or spaces of derivations, or whatever equivalent definition. Instead, one relies all the
time on functoriality (i.e., a smooth map has a well defined derivative, and they compose well,
together with the fact that the tangent bundle to a vector space is the product).

If you want to know more about the internals of the tangent bundle in mathlib, you can browse
through the next section, but it is maybe wiser to skip it on first reading, as it is not needed
to use the library
-/

section you_should_probably_skip_this

/- If `M` is a manifold modelled on a vector space `E`, then tangent bundle is defined as
  `TotalSpace E (TangentSpace I : M â†’ Type _)`. An element of this is a pair `âŸ¨x, vâŸ©` with `x : M`
  and `v : TangentSpace I x`.
  Here `TangentSpace I x` is just (a copy of) `E` by definition. -/

lemma tangentBundle_myâ„_eq_totalSpace :
    TangentBundle ğ“¡1 Myâ„ = Bundle.TotalSpace â„ (fun _x : Myâ„ â†¦ â„) :=
  rfl

/- However, in general, a tangent bundle is not trivial:
the topology on `TangentBundle ğ“¡1 Myâ„` is *not* the product topology.
Instead, the tangent space at a point `x` is identified
with `â„` through some preferred chart at `x`, called `chartAt â„ x`,
but the way they are glued together depends on the manifold and the charts.

In vector spaces, the tangent space is canonically the product space, with the same topology, as
there is only one chart so there is no strange gluing at play. The fact that the canonical map
from the sigma type to the product type (called `equiv.sigma_equiv_prod`) is a homeomorphism is
given in the library by `tangentBundleModelSpaceHomeomorph` (note that this is a definition,
constructing the homeomorphism, instead of a proposition asserting that `equiv.sigma_equiv_prod`
is a homeomorphism, because we use bundled homeomorphisms in mathlib).

Let us register the identification explicitly, as a homeomorphism. You can use the relevant fields
of `tangentBundleModelSpaceHomeomorph` to fill the nontrivial fields here.
-/

def tangentBundleVectorSpaceTriv (E : Type _) [NormedAddCommGroup E] [NormedSpace â„ E] :
  TangentBundle (modelWithCornersSelf â„ E) E â‰ƒâ‚œ E Ã— E where
  toFun := fun p â†¦ (p.1, p.2)
  invFun := fun p â†¦ âŸ¨p.1, p.2âŸ©
  left_inv := by
  right_inv := by
  continuous_toFun := by
  continuous_invFun := by

/- Even though the tangent bundle to `Myâ„` is trivial abstractly, with this construction the
tangent bundle is *not* the product space with the product topology, as we have used various charts
so the gluing is not trivial. The following exercise unfolds the definition to see what is going on.
It is not a reasonable exercise, in the sense that one should never ever do this when working
with a manifold! -/

lemma crazy_formula_after_identifications (x : â„) (v : â„) :
    let p : TangentBundle ğ“¡1 Myâ„ := âŸ¨(3 : â„), 0âŸ©
    chartAt (ModelProd â„ â„) p âŸ¨x, vâŸ© = if x âˆˆ Ioo (-1 : â„) 1 then (x, -v) else (x, v) := by
  -- this exercise is not easy (and shouldn't be: you are not supposed to use the library like this!)
  -- if you really want to do this, you should unfold as much as you can using simp and dsimp, until you
  -- are left with a statement speaking of derivatives of real functions, without any manifold code left.

end you_should_probably_skip_this

/-!
### The language of manifolds

In this paragraph, we will try to write down interesting statements of theorems, without proving them. The
goal here is that Lean should not complain on the statement, but the proof should be sorried.
-/

/- Here is a first example, already filled up, to show you how diffeomorphisms are currently named
(we will probably introduce an abbreviation, but this hasn't been done yet).
Don't try to fill the sorried proof! -/

/-- Two zero-dimensional connected manifolds are diffeomorphic. -/
theorem diffeomorph_of_zero_dim_connected
  (M M' : Type*) [TopologicalSpace M] [TopologicalSpace M']
  [ChartedSpace (EuclideanSpace â„ (Fin 0)) M] [ChartedSpace (EuclideanSpace â„ (Fin 0)) M']
  [ConnectedSpace M] [ConnectedSpace M'] :
  Nonempty (Structomorph (contDiffGroupoid âˆ (ğ“¡ 0)) M M') :=
sorry

/- Do you think that this statement is correct? (note that we have not assumed that our manifolds
are smooth, nor that they are separated, but this is maybe automatic in zero dimension).

Now, write down a version of this theorem in dimension 1, replacing the first sorry with meaningful content
(and adding what is needed before the colon).
Don't try to fill the sorried proof! -/

/-- Two one-dimensional smooth compact connected manifolds are diffeomorphic. -/
theorem diffeomorph_of_one_dim_compact_connected :
    sorry :=
  sorry

/- You will definitely need to require smoothness and separation in this case, as it is wrong otherwise.
Note that Lean won't complain if you don't put these assumptions, as the theorem would still make
sense, but it would just turn out to be wrong.

The previous statement is not really satisfactory: we would instead like to express that any such
manifold is diffeomorphic to the circle. The trouble is that we don't have the circle as a smooth
manifold yet. Since we have cheated and introduced it (with sorries) at the beginning of the tutorial,
let's cheat again and use it to reformulate the previous statement.
-/

notation "sphere" n => Metric.sphere (0 : EuclideanSpace â„ (Fin (n+1))) 1

-- The sphere is connected, compact and Hausdorff:

instance (n : â„•) : ConnectedSpace (sphere (n+1)) := by
  apply Subtype.connectedSpace
  apply isConnected_sphere
  Â· simp [â† finrank_eq_rank]
    norm_cast
    simp
  Â· exact zero_le_one

example (n : â„•) : T2Space (sphere n) := by
  infer_instance

example (n : â„•) : CompactSpace (sphere n) := by
  infer_instance

instance (n : â„•) : Fact (finrank â„ (EuclideanSpace â„ (Fin (n+1))) = n + 1) := by
  constructor
  simp_rw [finrank_euclideanSpace, Fintype.card_fin]

/- Now, you can prove that any one-dimensional compact connected manifold is diffeomorphic to
the circle. Here, you should fill the `sorry` (but luckily you may use
`diffeomorph_of_one_dim_compact_connected`). -/
theorem diffeomorph_circle_of_one_dim_compact_connected
    (M : Type*) [TopologicalSpace M] [ChartedSpace (EuclideanSpace â„ (Fin 1)) M]
    [ConnectedSpace M] [CompactSpace M] [T2Space M] [SmoothManifoldWithCorners (ğ“¡ 1) M] :
    Nonempty (Structomorph (contDiffGroupoid âˆ (ğ“¡ 1)) M (sphere 1)) := by
  exact diffeomorph_of_one_dim_compact_connected M (sphere 1)


/- What about trying to say that there are uncountably many different smooth structures on `â„â´`?
(see https://en.wikipedia.org/wiki/Exotic_R4). The library is not really designed with this in mind,
as in general we only work with one differentiable structure on a space, but it is perfectly
capable of expressing this fact if one uses the `@` version of some definitions.

Don't try to fill the sorried proof!
-/
theorem exotic_â„4 :
    sorry :=
  sorry

/-!
### Smooth functions on `[0, 1]`

In the following exercises we will prove that the tangent bundle to `[0, 1]`
is homeomorphic to `[0, 1] Ã— â„`. This is math-trivial but Lean-nontrivial.
These facts should also be Lean-trivial, but they are not (yet) since there is essentially
nothing in this direction for now in the library.

The goal is as much to be able to write the statements as to prove them. Most of the necessary vocabulary
has been introduced above, so don't hesitate to browse the file if you are stuck. Additionally, you will
need the notion of a smooth function on a subset: it is `ContDiffOn` for functions between vector
spaces and `ContMDiffOn` for functions between manifolds.

A global advice: don't hesitate to use and abuse `simp`, it is the main workhorse in this
area of mathlib.
-/

open unitInterval


def g : I â†’ â„ := Subtype.val

-- smoothness results for `EuclideanSpace` are expressed for general `L^p` spaces
-- (as `EuclideanSpace` has the `L^2` norm), in:
#check PiLp.contDiff_coord
#check PiLp.contDiffOn_iff_coord

lemma contMDiffG : ContMDiff (ğ“¡âˆ‚ 1) ğ“¡1 âˆ g := by

lemma msmooth_of_smooth {f : â„ â†’ I} {s : Set â„} (h : ContDiffOn â„ âˆ (fun x â†¦ (f x : â„)) s) :
  ContMDiffOn ğ“¡1 (ğ“¡âˆ‚ 1) âˆ f s := by

/- A function from `â„` to `[0,1]` which is the identity on `[0,1]`. -/
def f : â„ â†’ I :=
  fun x â†¦ âŸ¨max (min x 1) 0, by simp [le_refl, zero_le_one]âŸ©

lemma contMDiffOnF : ContMDiffOn ğ“¡1 (ğ“¡âˆ‚ 1) âˆ f I := by

lemma fog : f âˆ˜ g = id := by

lemma gof : âˆ€ x âˆˆ I, g (f x) = x := by

def G : TangentBundle (ğ“¡âˆ‚ 1) I â†’ I Ã— â„ :=
  fun p â†¦ (p.1, ((tangentBundleVectorSpaceTriv â„) (tangentMap (ğ“¡âˆ‚ 1) ğ“¡1 g p)).2)

lemma continuous_G : Continuous G := by

def F : I Ã— â„ â†’ TangentBundle (ğ“¡âˆ‚ 1) I :=
  fun p â†¦ tangentMapWithin ğ“¡1 (ğ“¡âˆ‚ 1) f I ((tangentBundleVectorSpaceTriv â„).symm (p.1, p.2))

lemma continuous_F : Continuous F := by

lemma FoG : F âˆ˜ G = id := by

lemma GoF : G âˆ˜ F = id := by

def myTangentHomeo : TangentBundle (ğ“¡âˆ‚ 1) I â‰ƒâ‚œ I Ã— â„ := by


/-!
### Further things to do

(1) can you prove `diffeomorph_of_zero_dim_connected`?

(2) Try to express and then prove the local inverse theorem in real manifolds: if a map between
real manifolds (without boundary, modelled on a complete vector space) is smooth, then it is
a local homeomorphism around each point. We already have versions of this statement in mathlib
for functions between vector spaces, but this is very much a work in progress.

(3) What about trying to prove `diffeomorph_of_one_dim_compact_connected`? (I am not sure mathlib
is ready for this, as the proofs I am thinking of are currently a little bit too high-powered.
If you manage to do it, you should absolutely PR it!)

(4) Construct the tensor product of two vector bundles
  Remark: we have not endowed the tensor product of vector spaces with a topology yet,
  even if the vector spaces were finite-dimensional.
-/
